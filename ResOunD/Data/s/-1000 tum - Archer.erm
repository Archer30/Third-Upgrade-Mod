ZVSE2
; Author:   Archer30
; Engine:   ERM 2.0+
; Requires: ERA 3.0+, Era Erm Framework, Third Upgrade Mod, WoG Scripts

; (Tiny little bit of) Archer's hacks for TUM


*****************************
**** Replace map objects ****
*****************************

!?FU(OnAfterErmInstructions)&i^tum_replaceObj_on^;
; deleting the sound in the delete object function
; because it causes a crash
!!UN:C4893167/2/?(patch:y);
!!UN:C4893167/2/24555;
; get size of the map
!!UN:X?(mapSize:y)/?(hasUnderground:y); 
; pass once through all cells of the map
!!re l/0/(hasUnderground)/1;  coord z
  !!re k/0/(mapSize)/1/-1;    coord y
    !!re i/0/(mapSize)/1/-1;  coord x
      !!OBi/k/l:T?(objType:y) U?(objSubtype:y);
      !!TRi/k/l:E?(isYellowSquare:y) P?(isPassable:y); 
      !!VR(isYellowSquare):X1;  reverse param isYellowSquare
      !!FU(tum_OnIterateAllMapObjects):Pi/k/l/(objType)/(objSubtype)/(isYellowSquare)/(isPassable);
    !!en;
  !!en;
!!en;
; restoring the source code of the sound
; in the delete objects function
!!UN:C4893167/2/(patch);

******************************
**** Fix Ring of Elements ****
******************************
; Allow summoning different elementals with Ring of Elements.
!?FU(OnGameEnter);
!!UN:C5896327/1/?i^tum_origElemLimit^;

!?FU(OnBattleRound)&i^battle_round^=0;
!!VRi^tum_elemRingDiff^:S(FALSE);

!!BA:A?(isOnlyAi:y) Q?(isQuick:y);
!!FU|(isOnlyAi)/(isQuick):E;

!!BA:H0/?(attacker:y) H1/?(defender:y);

!!HE(attacker):A2/(ART_RING_OF_ELEMENTS)/?(qty:y)/?(atkEquipped:y);
!!HE(defender)&(defender)>(NO_HERO):A2/(ART_RING_OF_ELEMENTS)/?(qty)/?(defEquipped:y);

!!FU(tum_ActivateElemRing)&(atkEquipped)/(defEquipped):P(TRUE);
!!FU(tum_ActivateElemRing)&(atkEquipped)=(FALSE)/(defEquipped)=(FALSE):P(FALSE);

!!VRi^tum_elemRingDiff^&(atkEquipped)<>(defEquipped):S(TRUE);

!?FU(OnBattleStackObtainsTurn)&i^tum_elemRingDiff^;
!#VA(stackSide:x) (stackInd:x);

!!BH(stackSide):N?(hero:y);

!!if&(hero)>(NO_HERO);
  !!HE(hero):A2/(ART_RING_OF_ELEMENTS)/?(qty:y)/?(equipped:y);
  !!FU(tum_ActivateElemRing):P(equipped);
!!en;

!?FU(tum_ActivateElemRing);
!#VA(equipped:x);

!!UN&(equipped)<>(FALSE):C5896327/1/235;
!!UN&(equipped)=(FALSE):C5896327/1/i^tum_origElemLimit^;

*************************************************************
**** Fix TUM werewolf faction with Mixed Neutrals option ****
*************************************************************

!?FU(OnAfterErmInited);
!!MA:O(MON_DIRE_WEREWOLF)/(TOWN_NECROPOLIS);

************************************************
**** Disable unused artifacts from the game ****
************************************************
; All the blank artifacts would be disabled if ACM is not presented
!?FU(tum_DisableUnusedArtifacts)&i^Advanced_Classes_Mod_Active^=(FALSE);
!!re i/(ART_BLANK_HELMET)/(ART_BLANK_HORN);
  !!UN:Ai/1;
!!en;

!#FU(tum_DisableUnusedArtifacts):P;

*****************************************
**** Fix Emerald Tower Compatibility ****
*****************************************
; The following lines must be executed later than WoG Scripts
!?FU(WOG_44_CheckIfMonDrainsLife);
!#VA(mon:x) (result:x);

!!VR(result)&(mon)=(MON_SHADOW_DRAGON):S(TRUE);

********************************************************
**** Enable Horn of the Abyss (artifact) for Utopia ****
********************************************************
; Allows HotA to be generated as one of the Utopia reward
!?OB(OBJ_DRAGON_UTOPIA);
!!FU(tum_SetHotAAvailability):P(TRUE);

!$OB(OBJ_DRAGON_UTOPIA);
!!FU(tum_SetHotAAvailability):P(FALSE);

; Rotton Dragon Valley
!?OB(OBJ_NEW_WOG_OBJECTS)/83;
!!FU(tum_SetHotAAvailability):P(TRUE);

!$OB(OBJ_NEW_WOG_OBJECTS)/83;
!!FU(tum_SetHotAAvailability):P(FALSE);

; Magic Utopia
!?OB(OBJ_NEW_WOG_OBJECTS)/84;
!!FU(tum_SetHotAAvailability):P(TRUE);

!$OB(OBJ_NEW_WOG_OBJECTS)/84;
!!FU(tum_SetHotAAvailability):P(FALSE);

; Ultimate Dragon Utopia
!?OB(OBJ_NEW_WOG_OBJECTS)/115;
!!FU(tum_SetHotAAvailability):P(TRUE);

!$OB(OBJ_NEW_WOG_OBJECTS)/115;
!!FU(tum_SetHotAAvailability):P(FALSE);

!?FU(tum_SetHotAAvailability)&i^tum_emerald_on^;
!#VA(isAvail:x);

!!if&(isAvail);
  !!UN:A(ART_HORN_OF_THE_ABYSS)/0;
  !!UN:A(ART_HORN_OF_THE_ABYSS)/3/(ART_LEVEL_RELIC);
!!el;
  !!UN:A(ART_HORN_OF_THE_ABYSS)/1;
  !!UN:A(ART_HORN_OF_THE_ABYSS)/3/(ART_LEVEL_SPECIAL);
!!en;

***************************************************
**** Match new monsters with their lower forms ****
***************************************************
!?FU(OnAfterErmInited);
; Match the faction of a few neutral creatures with new factions
; Crystal Dragon
!!MA:O(MON_RUBY_CRYSTAL_DRAGON)/?(faction:y);
!!MA:O(MON_TOPAZ_CRYSTAL_DRAGON)/(faction);
!!MA:O(MON_AMETHYST_CRYSTAL_DRAGON)/(faction);
!!MA:O(MON_EMERALD_CRYSTAL_DRAGON)/(faction);
!!MA:O(MON_SAPPHIRE_CRYSTAL_DRAGON)/(faction);

; Faerie Dragon
!!MA:O(MON_FAERIE_DRAGON)/?(faction);
!!MA:O(MON_PRIMAL_FAERIE_DRAGON)/(faction);

; Enchanter
!!MA:O(MON_ENCHANTER)/?(faction);
!!MA:O(MON_SPELLWEAVER)/(faction);

; Peasant
!!MA:O(MON_PEASANT)/?(faction);
!!MA:O(MON_CONSCRIPT)/(faction);
!!MA:O(MON_SQUIRE)/(faction);

; Rogue
!!MA:O(MON_ROGUE)/?(faction);
!!MA:O(MON_ASSASSIN)/(faction);

; Gorynych
!!MA:O(MON_GORYNYCH)/?(faction);
!!MA:O(MON_TIAMAT)/(faction);

; Dracolich
!!MA:O(MON_DRACOLICH)/?(faction);
!!MA:O(MON_NECROSS_DRAGON)/(faction);

***********************************************************************
**** Match the R10/R11 exp with Blood Dragon for level 8+ monsters ****
***********************************************************************
!?FU(OnAfterErmInited);
!!MA:F(MON_BLOOD_DRAGON)/?(bloodFightValue:y);
!!EA(MON_BLOOD_DRAGON):L?(r10Exp:y) P?(topExp:y);
!!FU(GetMaxMonsterId):P?(lastMon:y);

!!re i/(MON_FIRST_TUM)/(lastMon);
  !!MA:Fi/?(fightValue:y);

  !!EAi&(fightValue)>=(bloodFightValue):L(r10Exp) P(topExp);
!!en;

*****************************************************************
**** Dirty fix for Astral Spirit summoning massive Air Elem. ****
*****************************************************************
; Remove spells of Astral Spirits without a hero
!?FU(OnBattleRound)&i^battle_round^=0/i^battle_hero_1^=(NO_HERO)/i^battle_ai_1^;
!!re i/(BATTLE_DEFENDER_STACK_FIRST)/(BATTLE_DEFENDER_STACK_LAST);
  !!BMi:T?(type:y);

  !!BMi&(type)=(MON_ASTRAL_SPIRIT_D):E0;
!!en;

**************************************************************
**** Third Upgrade Mod Chinese Edition special treatments ****
**************************************************************
; The following lines works only for TUM Chinese Edition
; Dirty fix: Temporarily disable Emissary CBs as they appear to be buggy
!?FU(tum_DisableEmissaryCreatureBanks_Chn);
!!SN:L^wogcn.dll^/?(wogcn:y);
!!FU&(wogcn)=0:E;

!!re i/119/122;
  !!VRi^CB_%i_Replace_Objects^:S(FALSE);
!!en;

!#FU(tum_DisableEmissaryCreatureBanks_Chn):P;

*************************************************
**** Replace Dens of Thieves with Treehouses ****
*************************************************
; Here we uses Den of Thieves for the replacement of Treehouses as the original repalcent - Mage Schools does not fit in tiles
!?FU(OnAfterErmInstructions);
!!VRi^CB_133_Replace_0_Type_Number^:S(OBJ_DEN_OF_THIEVES);
!!VRi^CB_133_Replace_0_Percentage^:S15;

*******************************
**** Manage Creature Stats ****
*******************************
; Managing creature stats for gameplay or mod compatibility reasons
!?FU(OnAfterErmInited);
; Rebalance Centaur Stack Exp Bonus
; For some reason, Centaur has stronger growth on stack exp bonus damage than Centaur Captain, this script intended to nerf it
!!UN:P(WOG_OPT_STACK_EXPERIENCE)/?(stackExp:y);

!!if&(stackExp);
  ; Max Damage
  !!EA(MON_CENTAUR_CAPTAIN):B2/1/77/43/?(r0:y)/?(r1:y)/?(r2:y)/?(r3:y)/?(r4:y)/?(r5:y)/?(r6:y)/?(r7:y)/?(r8:y)/?(r9:y)/?(r10:y);
  !!EA(MON_CENTAUR):B2/1/77/43/(r0:y)/(r1:y)/(r2:y)/(r3:y)/(r4:y)/(r5:y)/(r6:y)/(r7:y)/(r8:y)/(r9:y)/(r10:y);
  ; Min Damage
  !!EA(MON_CENTAUR_CAPTAIN):B3/1/109/43/?(r0:y)/?(r1:y)/?(r2:y)/?(r3:y)/?(r4:y)/?(r5:y)/?(r6:y)/?(r7:y)/?(r8:y)/?(r9:y)/?(r10:y);
  !!EA(MON_CENTAUR):B3/1/109/43/(r0:y)/(r1:y)/(r2:y)/(r3:y)/(r4:y)/(r5:y)/(r6:y)/(r7:y)/(r8:y)/(r9:y)/(r10:y);
!!en;
