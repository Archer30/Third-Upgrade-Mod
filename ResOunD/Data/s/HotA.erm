ZVSE2
; Author:   Archer30
; Special thanks to Hawaiing
; Engine:   ERM 2.0+
; Requires: ERA 3.8.5+, Era Erm Framework

; Horn of the Abyss (artifact) for TUM


// Summon fangarms if a living stack is killed by a hero with HotA equipped
!?FU(OnBeforeBattleAction);
!!BG:E?i^hota_defStack^;

!?FU(OnBattleActionEnd)&i^hota_defStack^>(NO_STACK);
; Check if it was an action that could result in casualties
!!BG:A?(action:y);

!!if|(action)=(BATTLE_ACTION_HERO_CAST)/(action)=(BATTLE_ACTION_WALK_AND_ATTACK)/(action)=(BATTLE_ACTION_SHOOT)/(action)=(BATTLE_ACTION_MONSTER_CAST);
  ; Check if it was an event one player act against the other
  !!BG:N?(atkStack:y) Q?(atkSide:y);
  !!VR(defStack:y):Si^hota_defStack^;
  !!BM(defStack):I?(defSide:y);

  !!if&(atkSide)<>(defSide);
    ; Execute function for both sides
    !!FU(hota_SummonFangarmsOnTheBattlefield):P(atkSide)/(defStack);
    !!FU(hota_SummonFangarmsOnTheBattlefield):P(defSide)/(atkStack);
  !!en;
!!en;

!?FU(hota_SummonFangarmsOnTheBattlefield);
!#VA(atkSide:x) (defStack:x);

; Check if HotA is equipped
!!HEi^battle_hero_%(atkSide)^:A2/(ART_HORN_OF_THE_ABYSS)/?(num:y)/?(equipped:y);

!!if&(equipped);
  ; Check if the defending stack was killed
  !!BM(defStack):B?(startNum:y) N?(currNum:y) T?(type:y) F?(flags:y);
  !!VR(isAlive:y)S(flags) &(MON_FLAG_ALIVE);
  !!FU(hota_CheckIfEligibleToSummon):P(atkSide)/?(isEligible:y);

  !!if&(startNum)>0/(currNum)<=0/(type)<>(MON_FANGARM)/(isAlive)/(isEligible);
    ; Calculate the number of fangarm summoning
    !!BM(defStack):P?(defHp:y);
    !!VR(defTotalHp:y):S(startNum) *(defHp);
    !!MA:P(MON_FANGARM)/?(fangarmHp:y);
    !!VR(maxNum:y):S(startNum) :2;
    !!VR(summNum:y):S(defTotalHp) :(fangarmHp) F0/(maxNum);

    ; Summon the fangarms if the result is more than 0
    !!if&(summNum)>0;
      !!FU(hota_RemoveDeadBody):P(defStack)/(FALSE);
      !!SN:P^FNGRSUMM.wav^;
      !!BM(defStack):P?(pos:y);
      !!FU(tum_PlayCustomAnimation):P^SP16.def^/(pos);
      !!BU:S(MON_FANGARM)/(summNum)/(pos)/(atkSide)/-1/1 E(pos)/?(stack:y);
      !!BM(stack):B0 Fd|(MON_FLAG_SUMMONED);
    !!en;
  !!en;
!!en;

!?FU(hota_CheckIfEligibleToSummon);
!#VA(side:x) (isEligible:x);

!!VR(isEligible):S(FALSE);

!!UN:C6919200/4/?(value:y);
!!VR(address:y):S(side) *4 +21692 +(value);
!!UN:C(address)/4/?(stackNum:y);

!!VR(isEligible)&(stackNum)<=20:S(TRUE);

// Get the number of increased Fangarms after battle
!?FU(OnAfterBattleAction);
!!BU:C?(battleIsEnd:y);

!!if&(battleIsEnd);
  !!re i/(BATTLE_LEFT)/(BATTLE_RIGHT);
    !!FU(hota_GetFangarmsIncreasedAfterBattle):Pi;
  !!en;
!!en;

!?FU(hota_GetFangarmsIncreasedAfterBattle);
!#VA(side:x);

!!VRi^hota_fangarmIncreased_%(side)^:S0;
!!VR(firstStack:y):S(side) *21;
!!VR(lastStack:y):S(firstStack) +20;

!!re i/(firstStack)/(lastStack);
  !!BMi:T?(type:y);

  !!if&(type)=(MON_FANGARM);
    !!BMi:B?(startNum:y) N?(currNum:y);
    !!VR(fangarmIncreased:y):S(currNum) -(startNum);
    !!VRi^hota_fangarmIncreased_%(side)^&(fangarmIncreased)>0:+(fangarmIncreased);
  !!en;
!!en;

!?FU(OnAfterBattleUniversal);
!!FU(hota_GiveFangarmAfterBattle)&i^hota_fangarmIncreased_0^>0:P(BATTLE_LEFT);
!!FU(hota_GiveFangarmAfterBattle)&i^hota_fangarmIncreased_1^>0:P(BATTLE_RIGHT);

!?FU(hota_GiveFangarmAfterBattle);
!#VA(side:x);

!!VR(hero:y):Si^battle_hero_%(side)^;

!!if&(hero)>(NO_HERO);
  !!HE(hero):O?(owner:y);

  !!if&(owner)>(NO_OWNER);
    !!VR(mode:y):S1;
    !!OW:I(owner)/?(isAi:y);
    !!VR(mode)&(isAi):S0;
    !!HE(hero):C2/(MON_FANGARM)/i^hota_fangarmIncreased_%(side)^/(mode);
    !!SN:D;
  !!en;
!!en;

// Hawaiing's memory hacks
!?FU(hota_RemoveDeadBody);
!#VA(stack:x);                          [stack id of the dead body]
!#VA(removeStack:x);                    [whether remove the stack completely]

!!FU|x1<0/x1>41/x2<0/x2>1:E;
!!BMx1:T?y1 N?y2 I?y3 F?i;
!!VRy5:Si &1;
!!VRy6:Si &8388608;
!!VRy7:Si &4194304;
!!VRy8:Si &268435456;
!!FU|y1<0/y1=149/y2>0/y6>0/y8>0:E;      [特殊生物退出]

!!UN:C6919200/4/?y13;                   [调用内存]
!!VRy14:Sx1 *1352 +21708 +y13;
!!SN:E4481984/2/y14;
!!BMx1:P?y15;
!!VRy16:Sv1;                            [取得另一坐标位置]

!!BU:Ey15/?y17;
!!FU&y17>-1:E;

!!if&y5=1;                              [处理双格死亡生物]
  !!BU:Ey16/?y18;
  !!FU&y18>-1:E;
!!en;

!!BMx1&x2=1/y7=0:Fd4194304;
!!BMx1&y8=0:Fd268435456;

!!VRy9:Sy3 *21 +x1;
!!VRy10:Sy15 *112 +y13 +452;
!!SN:E5928496/1/y10/y3/y9;

!!if&y5=1;
  !!VRy11:Sy16 *112 +y13 +452;
  !!SN:E5928496/1/y11/y3/y9;
!!en;
